<html>
<head>
<script type="text/ohm-js">

O {

  Program
    = Stmts

  Stmts
    = Stmt* Exp?

  Stmt
    = class ident (extends ident)? (with NonemptyListOf<ident, ",">)? ";"  -- classDecl
    | def ident "." ident "(" ListOf<ident, ","> ")" MethodBody            -- methodDeclJava
    | def ident (ident ":" ident)+ MethodBody                              -- methodDeclKeyword
    | def ident binSelector ident MethodBody                               -- methodDeclBinary
    | var ident "=" Exp ";"                                                -- varDecl
    | ident "=" Exp ";"                                                    -- varAssign
    | this "." ident "=" Exp ";"                                           -- instVarAssign
    | return Exp ";"                                                       -- return
    | Exp ";"                                                              -- exp

  MethodBody
    = "=" Exp ";"    -- exp
    | "{" Stmts "}"  -- stmt

  Exp
    = KWSendExp

  KWSendExp
    = EqExp (ident ":" EqExp)+  -- send
    | super (ident ":" EqExp)+  -- super
    | EqExp

  EqExp
    = RelExp ("==" | "!=") RelExp  -- eq
    | RelExp

  RelExp
    = AddExp ("<=" | "<" | ">=" | ">") AddExp  -- rel
    | AddExp

  AddExp
    = AddExp ("+" | "-") MulExp  -- add
    | MulExp

  MulExp
    = MulExp ("*" | "/" | "%") DotExp  -- mul
    | DotExp

  DotExp
    = DotExp "." ident "(" Actuals ")"  -- send
    | super "." ident "(" Actuals ")"   -- super
    | this "." ident  ~"("              -- instVarAccess
    | UnExp

  UnExp
    = "-" PriExp  -- neg
    | PriExp

  PriExp
    = "(" Exp ")"                  -- paren
    | "{" BlockArgNames Stmts "}"  -- block
    | new ident "(" Actuals ")"    -- new
    | string                       -- str
    | ident                        -- ident
    | number                       -- number
    | this                         -- this
    | trueK                        -- true
    | falseK                       -- false
    | nullK                        -- null

  Actuals
    = ListOf<Exp, ",">

  BlockArgNames
    = ListOf<ident, ","> "|"  -- some
    |                         -- none

  // Lexical rules

  ident  (an identifier)
    = ~keyword letter alnum*

  string  (a string literal)
    = "\"" (~"\"" ~"\n" any)* "\""

  number  (a number literal)
    = digit* "." digit+  -- fract
    | digit+             -- whole

  binSelector  (a binary selector)
    = "===" | "==" | "!==" | "!=" | "<=" | "<" | ">=" | ">"
    | "+"   | "-"  | "*"   | "/"  | "%"

  class = "class" ~alnum
  def = "def" ~alnum
  else = "else" ~alnum
  extends = "extends" ~alnum
  falseK = "false" ~alnum
  if = "if" ~alnum
  new = "new" ~alnum
  nullK = "null" ~alnum
  return = "return" ~alnum
  super = "super" ~alnum
  then = "then" ~alnum
  this = "this" ~alnum
  trueK = "true" ~alnum
  var = "var" ~alnum
  with = "with" ~alnum

  keyword
    = class | def | else | extends | falseK | if | new | nullK | return | super
    | then | this | trueK | var | with

  space
   += comment

  comment
    = "/*" (~"*/" any)* "*/"  -- multiLine
    | "//" (~"\n" any)*       -- singleLine

  tokens
    = (keyword | ident | number | comment | any)*

}


</script>
<script src="ohm.js"></script>
<script src="infer.js"></script>
<style>
columns {
  display: flex;
  flex-direction:row;
  width: 700px;
}

column {
  display: block;
  height: 100%;
  min-width: 500px;
  padding-left: 10px;
}
</style>
</head>

<body>
<columns>
<column>
  <pre>
    O {

      Program
        = Stmts

      Stmts
        = Stmt* Exp?

      Stmt
        = class ident (extends ident)? (with NonemptyListOf<ident, ",">)? ";"  -- classDecl
        | def ident "." ident "(" ListOf<ident, ","> ")" MethodBody            -- methodDeclJava
        | def ident (ident ":" ident)+ MethodBody                              -- methodDeclKeyword
        | def ident binSelector ident MethodBody                               -- methodDeclBinary
        | var ident "=" Exp ";"                                                -- varDecl
        | ident "=" Exp ";"                                                    -- varAssign
        | this "." ident "=" Exp ";"                                           -- instVarAssign
        | return Exp ";"                                                       -- return
        | Exp ";"                                                              -- exp

      MethodBody
        = "=" Exp ";"    -- exp
        | "{" Stmts "}"  -- stmt

      Exp
        = KWSendExp

      KWSendExp
        = EqExp (ident ":" EqExp)+  -- send
        | super (ident ":" EqExp)+  -- super
        | EqExp

      EqExp
        = RelExp ("==" | "!=") RelExp  -- eq
        | RelExp

      RelExp
        = AddExp ("<=" | "<" | ">=" | ">") AddExp  -- rel
        | AddExp

      AddExp
        = AddExp ("+" | "-") MulExp  -- add
        | MulExp

      MulExp
        = MulExp ("*" | "/" | "%") DotExp  -- mul
        | DotExp

      DotExp
        = DotExp "." ident "(" Actuals ")"  -- send
        | super "." ident "(" Actuals ")"   -- super
        | this "." ident  ~"("              -- instVarAccess
        | UnExp

      UnExp
        = "-" PriExp  -- neg
        | PriExp

      PriExp
        = "(" Exp ")"                  -- paren
        | "{" BlockArgNames Stmts "}"  -- block
        | new ident "(" Actuals ")"    -- new
        | string                       -- str
        | ident                        -- ident
        | number                       -- number
        | this                         -- this
        | trueK                        -- true
        | falseK                       -- false
        | nullK                        -- null

      Actuals
        = ListOf<Exp, ",">

      BlockArgNames
        = ListOf<ident, ","> "|"  -- some
        |                         -- none

      // Lexical rules

      ident  (an identifier)
        = ~keyword letter alnum*

      string  (a string literal)
        = "\"" (~"\"" ~"\n" any)* "\""

      number  (a number literal)
        = digit* "." digit+  -- fract
        | digit+             -- whole

      binSelector  (a binary selector)
        = "===" | "==" | "!==" | "!=" | "<=" | "<" | ">=" | ">"
        | "+"   | "-"  | "*"   | "/"  | "%"

      class = "class" ~alnum
      def = "def" ~alnum
      else = "else" ~alnum
      extends = "extends" ~alnum
      falseK = "false" ~alnum
      if = "if" ~alnum
      new = "new" ~alnum
      nullK = "null" ~alnum
      return = "return" ~alnum
      super = "super" ~alnum
      then = "then" ~alnum
      this = "this" ~alnum
      trueK = "true" ~alnum
      var = "var" ~alnum
      with = "with" ~alnum

      keyword
        = class | def | else | extends | falseK | if | new | nullK | return | super
        | then | this | trueK | var | with

      space
       += comment

      comment
        = "/*" (~"*/" any)* "*/"  -- multiLine
        | "//" (~"\n" any)*       -- singleLine

      tokens
        = (keyword | ident | number | comment | any)*

    }
  </pre>
</column>

<column>
<pre id="example">
class Point with x, y;

def Point.init(x, y) {
  this.x = x;
  this.y = y;
}

def Point.toString() {
  return "Point(" + this.x + ", " + this.y + ")";
}


class ThreeDeePoint extends Point with z;

def ThreeDeePoint.init(x, y, z) {
  super.init(x, y);
  this.z = z;
}

def ThreeDeePoint.toString() {
  return "ThreeDeePoint(" +
      this.x + ", " +
      this.y + ", " +
      this.z + ")";
}
</pre>
</column>
</columns>

<script src="../lib/ohm.js"></script>
<script src="reify.js"></script>
<script>
'use strict';

document.addEventListener("DOMContentLoaded", function(event) {
  let grammar = ohm.grammarFromScriptElement();

  let exampleNode = document.querySelector('pre#example');
  let example = exampleNode.textContent;
  let reified = reify(grammar, example);
  let DOM = reified[0],
      domToOhm = reified[1],
      ohmToDom = reified[2];

  exampleNode.textContent = "";
  exampleNode.appendChild(DOM);
});
</script>
</body>
</html>
