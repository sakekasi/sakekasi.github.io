<html>
<head>
  <meta charset='utf-8'>
  <link rel="stylesheet" type="text/css" href="lib/codemirror-5.11/lib/codemirror.css">
  <link rel="stylesheet" type="text/css" href="dist/semanticEditor.css">
  <script type="text/ohm-js">
O {

  Program
    = Stmts

  Stmts
    = Stmt* Exp?

  Stmt
    = class ident (extends ident)? (with NonemptyListOf<ident, ",">)? ";"  -- classDecl
    | def ident "." ident "(" ListOf<ident, ","> ")" MethodBody                -- methodDeclJava
    | def ident (ident ":" ident)+ MethodBody                                       -- methodDeclKeyword
    | def ident binSelector ident MethodBody                                         -- methodDeclBinary
    | def ident "(" ListOf<ident, ","> ")" MethodBody                             -- methodDeclCall
    | var ident "=" Exp ";"                                                              -- varDecl
    | ident "=" Exp ";"                                                                   -- varAssign
    | this "." ident "=" Exp ";"                                                        -- instVarAssign
    | return Exp ";"                                                                       -- return
    | Exp ";"                                                                                -- exp

  MethodBody
    = "=" Exp ";"    -- exp
    | "{" Stmts "}"  -- stmt

  Exp
    = KWSendExp

  KWSendExp
    = EqExp (ident ":" EqExp)+  -- send
    | super (ident ":" EqExp)+  -- super
    | EqExp

  EqExp
    = RelExp ("==" | "!=") RelExp  -- eq
    | RelExp

  RelExp
    = AddExp ("<=" | "<" | ">=" | ">") AddExp  -- rel
    | AddExp

  AddExp
    = AddExp ("+" | "-") MulExp  -- add
    | MulExp

  MulExp
    = MulExp ("*" | "/" | "%") DotExp  -- mul
    | DotExp

  DotExp
    = DotExp "." ident "(" Actuals ")"  -- send
    | super "." ident "(" Actuals ")"   -- super
    | this "." ident  ~"("              -- instVarAccess
    | UnExp

  UnExp
    = "-" CallExp  -- neg
    | CallExp

  CallExp
    = CallExp "(" Actuals ")"  -- call
    | PriExp

  PriExp
    = "(" Exp ")"                  -- paren
    | "{" BlockArgNames Stmts "}"  -- block
    | new ident "(" Actuals ")"    -- new
    | string                       -- str
    | ident                        -- ident
    | number                       -- number
    | this                         -- this
    | trueK                        -- true
    | falseK                       -- false
    | nullK                        -- null

  Actuals
    = ListOf<Exp, ",">

  BlockArgNames
    = ListOf<ident, ","> "|"  -- some
    |                         -- none

  // Lexical rules

  ident  (an identifier)
    = ~keyword letter alnum*

  string  (a string literal)
    = "\"" (~"\"" ~"\n" any)* "\""

  number  (a number literal)
    = digit* "." digit+  -- fract
    | digit+             -- whole

  binSelector  (a binary selector)
    = "==" | "!=" | "<=" | "<" | ">=" | ">" | "+"  | "-"  | "*"  | "/" | "%" | "@"

  class = "class" ~alnum
  def = "def" ~alnum
  extends = "extends" ~alnum
  falseK = "false" ~alnum
  new = "new" ~alnum
  nullK = "null" ~alnum
  return = "return" ~alnum
  super = "super" ~alnum
  this = "this" ~alnum
  trueK = "true" ~alnum
  var = "var" ~alnum
  with = "with" ~alnum

  keyword
    = class | def | extends | falseK | new | nullK | return | super | this | trueK | var | with

  space
   += comment

  comment
    = "/*" (~"*/" any)* "*/"  -- multiLine
    | "//" (~"\n" any)*       -- singleLine

  tokens
    = (keyword | ident | number | comment | any)*

}
  </script>
</head>
<body>
<columns>





<column id="examples">
<example>
<exampleText>
<pre id="example">
def True then: run else: skip {
  run.call();
}

def False then: skip else: run {
  run.call();
}

def Number.fibonacci(i) {
  (i==0) then: {
    return 0;
  } else: {
    (i==1) then: {
      return 1;
    } else: {
      return (this-1).fibonacci() +
             (this-2).fibonacci();
    }
  }
}

52.fibonacci()
</pre>
  <!-- example's text -->
</exampleText>
<exampleOverlay><svg></svg></exampleOverlay>
</example>
</column>

<column id="exampleInput">
  <!-- Example Text -->
  <editor></editor>
  <errorOutput></errorOutput>
</column>


<column id="grammarSemantics" style="display: none">
  <div class="flex-fix">
    <div class="editorWrapper"></div>
  </div>
</column>


</columns>

<script src="lib/codemirror-5.11/lib/codemirror.js"></script>
<script src="lib/ohm.js"></script>
<!-- <script src="lib/d3.min.js"></script> -->
<script src="dist/commons.js"></script>
<script src="dist/semanticEditor.bundle.js"></script>

</body>
</html>
